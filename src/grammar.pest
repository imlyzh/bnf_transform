unit = { (!EOI ~ production)* ~ EOI }

production = { production_name ~ "=" ~ expression ~ "." }

expression = { alternative ~ ("|" ~ alternative)* }

alternative = { term+ }

term =
	{ production_name
	| token_pair
	| group
	| option
	| repetition
	}

group = { "(" ~ expression ~ ")" }
option = { "[" ~ expression ~ "]" }
repetition = { "{" ~ expression ~ "}" }

production_name = ${ (!(WHITESPACE | "…" | "..." | "." | "|" | "=" | "\"" | "\'" | "(" | ")" | "[" | "]" | "{" | "}") ~ ANY)+ }

token_pair = { token ~ (("…"|"...") ~ token)? }

token = { "\"" ~ (!"\"" ~ ANY)+ ~ "\"" }

COMMENT = _{ "/*" ~ (COMMENT | (!"*/" ~ ANY))* ~ "*/" }

WHITESPACE = _
	{ " "
	| "\t"
    | "\n"
	| WHITE_SPACE
	| NEWLINE
	}